" Variables {{{1

" Disable compability with old fashioned Vi
set nocompatible

" Setup directory paths
if has('nvim')
  let s:config_dir = expand(has('win32') ? '$LOCALAPPDATA/nvim' : '~/.config/nvim')
else
  let s:config_dir = expand(has('win32') ? '$USERPROFILE/vimfiles' : '~/.vim')
endif
let s:cache_dir = s:config_dir . '/.cache'
let s:plugins_dir = s:config_dir . '/plugged'

" Setup global variables
let g:use_fzf = executable('fzf') && !has('gui_running') && !has('win32')

" Functions {{{1

function! s:RunFZF(command, ...) " {{{2
  if &buftype == 'quickfix' || &buftype == 'help'
    wincmd p
  endif
  execute a:command join(a:000, ' ')
endfunction

" }}}2

" Plugins {{{1

" Begin the plugin section
execute 'source ' . fnameescape(s:plugins_dir . '/vim-plug/plug.vim')
call plug#begin(s:plugins_dir)

" List the required plugins
Plug 'Valloric/ListToggle'
Plug 'FelikZ/ctrlp-py-matcher', !g:use_fzf ? {} : {'on': []}
Plug 'ctrlpvim/ctrlp.vim', !g:use_fzf ? {} : {'on': []}
Plug 'Raimondi/delimitMate'
Plug 'junegunn/fzf', g:use_fzf ? {} : {'on': []}
Plug 'junegunn/fzf.vim', g:use_fzf ? {} : {'on': []}
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'kana/vim-altr'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-dispatch'
Plug 'mhinz/vim-grepper'
Plug 'toiffel/vim-hybrid'
Plug 'embear/vim-localvimrc'
Plug 'dbakker/vim-projectroot'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-surround'
Plug 'tmux-plugins/vim-tmux-focus-events'
Plug 'christoomey/vim-tmux-navigator'
Plug 'tpope/vim-unimpaired'
Plug 'thinca/vim-visualstar'

" Finish the plugin section, update runtimepath and initialize plugin system
call plug#end()

" General settings {{{1

" Set the default character encoding
if !has('nvim')
  set encoding=utf-8
endif

" Don't clutter the filesystem with garbage files
set nobackup
set nowritebackup
set noswapfile
set noundofile

" Put unsaved buffers to background when loading new buffers
" without asking to save the current changes first
set hidden

" Filetype-specific settings
augroup vimrc
  autocmd!
  autocmd FileType text setlocal wrap
  autocmd FileType cpp setlocal commentstring=//\ %s
  autocmd FileType haskell,cabal setlocal expandtab
augroup end

" UI settings {{{1

" Use dark variation of color scheme
set background=dark

" Set some eye candy color scheme
colorscheme hybrid

" Enable 24-bit colors in the terminal
set termguicolors

" Define 24-bit TrueColor RGB escape sequences
let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"

" Set font when running in GUI mode
if has('gui_running')
  if has('gui_gtk')
    set guifont=Droid\ Sans\ Mono\ Slashed\ for\ Powerline\ 14,Monospace\ 14
  elseif has('gui_macvim')
    set guifont=Menlo\ Regular:h18
  elseif has('gui_win32')
    set guifont=Powerline\ Consolas:h14:cANSI,Consolas:h14:cANSI
  endif
endif

" Remove menu bar, toolbar and scrollbars
if has('gui_running')
  set guioptions-=m
  set guioptions-=T
  set guioptions-=l
  set guioptions-=L
  set guioptions-=r
  set guioptions-=R
  set guioptions-=b
endif

" Configure the cursor shape
if has('gui_running')
  set guicursor+=o:block-Cursor,a:blinkon0
elseif has('nvim')
  let $NVIM_TUI_ENABLE_CURSOR_SHAPE = 1
else
  let &t_SI = "\<Esc>[6 q"
  let &t_SR = "\<Esc>[4 q"
  let &t_EI = "\<Esc>[2 q"
endif

" Turn off annoying beeps/flashes on errors
set noerrorbells
set novisualbell
set t_vb=

" Don't redraw the display while executing macros
set lazyredraw

" Disable soft wrapping of long lines
set nowrap

" Disable hard wrapping of long lines
set textwidth=0

" Show absolute number for the current line and relative numbers for other lines
set number
set relativenumber

" Highlight the line containing the cursor
set cursorline

" Keep the given number of lines above and below the cursor while scrolling
set scrolloff=5

" Enable list mode, show whitespace characters
set listchars=tab:»\ ,space:·,trail:·,extends:>,precedes:<
set list

" Don't show the current mode on the last line
set noshowmode

" Show the current command in the bottom right corner
set showcmd

" Edit settings {{{1

" Set width of tabs and indents to 4 spaces
set tabstop=4
set softtabstop=4
set shiftwidth=4

" Always use tabs instead of spaces
set noexpandtab

" Disable mouse support
set mouse=

" Allow backspacing over autoindent, line break and the start of insert
" Required to avoid an error message from delimitMate upon startup
set backspace=indent,eol,start

" Keep the cursor column while jumping or switching between buffers
set nostartofline

" Search settings {{{1

" Highlight all matches of the search pattern
set hlsearch

" Ignore case in search patterns
set ignorecase

" When searching, wrap around the beginning or the end of the file
set wrapscan

" Don't jump to matching brackets while typing
set noshowmatch

" Key mappings {{{1

" Configure the <Leader> key
let mapleader = "\<Space>"

" Edit/reload Vim configuration file
nnoremap <silent> <Leader>ve :edit $MYVIMRC<CR>
nnoremap <silent> <Leader>vr :source $MYVIMRC<CR>

" Fast saving
nnoremap <silent> <Leader>w :write<CR>

" Clear the last search highlighting
nnoremap <silent> <Leader><Space> :nohlsearch<CR>

" Move cursor up/down by screen lines when wrapping the text
nnoremap <expr> j v:count == 0 ? 'gj' : 'j'
nnoremap <expr> k v:count == 0 ? 'gk' : 'k'

" Make Y yank from the current cursor position to the end of line,
" to be consistent with C and D
nnoremap Y y$

" Jump to the current error in quickfix or location list
nnoremap <silent> =q :cc<CR>
nnoremap <silent> =l :ll<CR>

" Plugin settings {{{1

" ctrlp.vim {{{2

if !g:use_fzf
  " Set the directory to store cache files
  let g:ctrlp_cache_dir = s:cache_dir . '/ctrlp'

  " Don't jump to buffer if it's already opened in a window somewhere
  " This is useful when opening the same buffer in different windows
  let g:ctrlp_switch_buffer = 0

  " Do not limit the maximum number of files to scan
  let g:ctrlp_max_files = 0

  " Use rg to speed up file indexing
  if executable('rg')
    let g:ctrlp_user_command = 'rg --files --hidden -g "!.git/*" %s'
  endif

  " Use Python-based matcher to speed up matching
  if has('python') || has('python3')
    let g:ctrlp_match_func = {'match': 'pymatcher#PyMatch'}
  endif

  " Search in files
  let g:ctrlp_map = '<Leader>f'

  " Search in the currently opened buffers
  nnoremap <silent> <Leader>b :CtrlPBuffer<CR>
endif

" delimitMate {{{2

" Insert an indent when pressing <CR> inside empty pair of matching characters
let delimitMate_expand_cr = 1

" fzf.vim {{{2

if g:use_fzf
  " Configure FZF window position and height
  let g:fzf_layout = {'down': '12'}

  " Use rg to speed up file indexing
  if executable('rg') && empty($FZF_DEFAULT_COMMAND)
    let $FZF_DEFAULT_COMMAND = 'rg --files --hidden -g "!.git/*"'
  endif

  " Search in files
  nnoremap <silent> <Leader>f :call <SID>RunFZF('FZF', fnameescape(projectroot#guess()))<CR>

  " Search in the currently opened buffers
  nnoremap <silent> <Leader>b :call <SID>RunFZF('Buffers')<CR>
endif

" netrw {{{2

" Hide annoying banner at the top of the window
let g:netrw_banner = 0

" Sort directories first, then files
let g:netrw_sort_sequence = '[\/]$,*'

" Show the directory listing in the current window
nnoremap <silent> - :Explore<CR>

" Refresh file list with R since <C-L> is used for navigation between windows
autocmd vimrc FileType netrw nmap <buffer> r <Plug>NetrwRefresh

" vim-airline {{{2

" Pick a good airline theme matching current Vim color scheme
let g:airline_theme = 'tomorrow'

" Use fancy powerline symbols for better looking
let g:airline_powerline_fonts = 1

" Disable the unwanted extensions
let g:airline#extensions#po#enabled = 0
let g:airline#extensions#whitespace#enabled = 0
let g:airline#extensions#wordcount#enabled = 0

" vim-altr {{{2

" Switch between associated files, e.g. C++ header and implementation
nmap <Leader>a <Plug>(altr-forward)

" Define additional vim-altr rules
call altr#define('%/src/%.c', '%/src/%.cpp', '%/src/%.cc',
  \ '%/src/%.m', '%/src/%.mm', '%/include/%.h', '%/include/%.hpp')
call altr#define('%/Sources/%.c', '%/Sources/%.cpp', '%/Sources/%.cc',
  \ '%/Sources/%.m', '%/Sources/%.mm', '%/Include/%.h', '%/Include/%.hpp')

" vim-dispatch {{{2

" Save all modified files and build the project
nnoremap <silent> <Leader>m :wall <Bar> Make<CR>

" vim-grepper {{{2

" Initialize vim-grepper options
runtime autoload/grepper.vim

" Highlight all matches of the search pattern
let g:grepper.highlight = 1

" Start searching from the project root (if any exists)
" Use the directory of the current buffer as a fallback
let g:grepper.dir = 'repo,file'

" Use rg as a primary search tool, grep or findstr as a fallback
let g:grepper.tools = ['rg', 'grep', 'findstr']

" Ignore case in search patterns
for tool in g:grepper.tools
  let g:grepper[tool].grepprg = substitute(g:grepper[tool].grepprg, ' ', ' -i ', '')
endfor

" Display Grepper prompt and wait for search pattern
nnoremap <Leader>/ :Grepper<CR>

" Search for currently selected text
xmap <Leader>* <Plug>(GrepperOperator)

" Search for word under the cursor
nnoremap <silent> <Leader>* :Grepper -cword -noprompt<CR>

" vim-localvimrc {{{2

" Don't load local vimrc files in a sandbox
let g:localvimrc_sandbox = 0

" Don't ask before sourcing local vimrc files
let g:localvimrc_ask = 0

" }}}2

" vim: fen fdm=marker fmr={{{,}}} fdc=3 et ts=2 sts=2 sw=2
